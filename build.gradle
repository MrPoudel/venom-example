// gradle build script for the VENOM architecture example
//
// free software - without guarantee, use at your own risk
// ========================================================

println """compiling with Java ${System.getProperty("java.version")} at ${new Date().format("HH:mm:ss")}"""


buildscript {
    // these are the BUILDSCRIPT deps - required to execute
    // build targets and -operations
    repositories {
      maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }


    dependencies {
        classpath ( group: 'gradle.plugin.org.aim42',
                    name: 'htmlSanityCheck',
                    version: '0.9.7')

        classpath ( group: 'org.asciidoctor',
                    name: 'asciidoctor-gradle-plugin',
                    version: '1.5.3')
    }
}


ext {
    srcDir  = "$projectDir/doc"
    targetDir = "$buildDir"
    javaVersion = System.getProperty("java.version")
    currentDate = new Date().format("d. MMM yyyy")
}


// where HTMLSanityCheck checking results ares stored
def checkingResultsPath = "$buildDir/report/htmlchecks"


// =========== asciidoctor stuff ===============
apply plugin: 'org.asciidoctor.convert'

asciidoctorj {
       version = '1.5.4'
    }

asciidoctor {
    outputDir = file( targetDir )
    sourceDir = file("$srcDir")
    println "outputDir = " + outputDir
    println "sourceDir = " + sourceDir

    sources  { include "architecture/arc42-venom.adoc",
                       "organization/index-orga.adoc" }

    backends 'html5' //,'pdf'

    attributes = [
            doctype: 'book',
            icons: 'font',
            toc: 'left',
            sectlink : true,
            sectanchors : true,
            numbered : true,
            'source-highlighter': 'coderay',
            //'imagesdir'         :'./images',
            'javaVersion'       : "$javaVersion",
            'currentDate'       : "$currentDate"

   ]


  //  println "arcImageDir = " + "${srcDir}/architecture/images"

    resources {
        // include architecture diagrams
        from("${srcDir}/architecture") {
          include 'images/**'
        }
        from("${srcDir}/organization") {
          include 'images/**'
        }
        // include logos and other common images
        from("${srcDir}") {
          include 'images/**'
        }
    }


}


defaultTasks 'asciidoctor'

// ========================================================
apply plugin: 'org.aim42.htmlSanityCheck'


htmlSanityCheck {

    // ensure asciidoctor->html runs first
    // and images are copied to build directory

    dependsOn asciidoctor

    sourceDir = new File( "$targetDir/html5" )

    // files to check, in Set-notation
    sourceDocuments = [ "architecture/arc42-venom.html",
                        "organization/index-orga.html"]

    // where to put results of sanityChecks...
    checkingResultsDir = new File( checkingResultsPath )

    // false: restrict checks to local resources
    // true:  also check external (e.g. http, https...) links.
    checkExternalLinks = false
}
